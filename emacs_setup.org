#+TITLE: Emacs configuration file
#+AUTHOR: Pascal Fleury
#+BABEL: :cache yes
#+PROPERTY: header-args :tangle yes

This is my Emacs config. You need to tangle this into a file that
then gets loaded by Emacs: =C-c C-v t= [org-babel-tangle].
Below, I also explain how this tangling is automated.

Extensive documentation about how to do this can be found [[https://github.com/larstvei/dot-emacs][here]].

* Setup
I have my config in directory =~/Emacs= which is where I clone this repository.
The config setup is maintained purely in the =~/Emacs/emacs_setup.org= file.

In your =~/.emacs= file, all you need to add is

#+BEGIN_SRC emacs-lisp :tangle no
(load-file "~/Emacs/emacs_setup.el")
#+END_SRC

** bootstrap
Initially when cloning this repository, you have the =emacs_setup.org= file,
that contains you adapt to your specific setup, and an =emacs_setup.el= with a
bootstrap content that will tangle and compile the org file, /and replace
itself/.

After that, the config itself should have the hook to re-tangle and re-compile
the setup at each save.

Therefore, my setup is very easy to install, goes in these step:

1. clone this repo into =~/Emacs=
2. add the one line in you =~/.emacs=
3. make sure Emacs re-interprets its init (you could restart it)

It may be that =[[https://github.com/jwiegley/use-package][use-package]] = is not installed oon your setup, so you will have
to add it.

#+BEGIN_SRC emacs-lisp :tangle no
;; This file replaces itself with the actual configuration at first run.

(require 'org) ; We can't tangle without org!

(find-file "~/Emacs/emacs_setup.org") ; Open the configuration
(org-babel-tangle)                    ; tangle it
(load-file "~/Emacs/emacs_setup.el")  ; load it
(byte-compile-file "emacs_setup.el")  ; finally byte-compile it
#+END_SRC

* Plans
I regularly try out new packags, and here is the current list of things I am
trying out.  They usually live temporarily in my =~/.emacs= untilI am happy, in
which case I move their config into this file so that it gets replicated on all
machines I work with emacs.

** Currently under review
  - org-gcal
  - plantuml-mode
  - ox-reveal (see [[https://github.com/yjwen/org-reveal#set-the-location-of-revealjs][installation]])
  - yankpad + yasnippet

** Installed, but need no or external config
- hc-zenburn-theme

* Initialize
** melpa
Make sure we have the package system initialized before we load anything.
#+BEGIN_SRC emacs-lisp
(require 'package)
(package-initialize)
#+END_SRC

Adding my own packages repositories.
#+BEGIN_SRC emacs-lisp
(setq package-archives '(("org" . "http://orgmode.org/elpa/")
                         ("melpa" . "http://melpa.org/packages/")
                         ("gnu" . "http://elpa.gnu.org/packages/")
                         ("marmalade" . "http://marmalade-repo.org/packages/")
                        ))
#+END_SRC
** use-package
I use =use-package= for most configuration, and that needs to be at the top of
the file.  +use-package= ensures the package is installed, if not installs it,
and has very convenient sections for configs of variables, key bindings
etc. that happen only if the package is actually loaded.
#+BEGIN_SRC emacs-lisp
(eval-when-compile (require 'use-package))
#+END_SRC
** add-hook-run-once
Use instead of add-hook to run it a single time.
[[https://emacs.stackexchange.com/questions/3323/is-there-any-way-to-run-a-hook-function-only-once][found here]]
#+BEGIN_SRC emacs-lisp
(defmacro add-hook-run-once (hook function &optional append local)
  "Like add-hook, but remove the hook after it is called"
  (let ((sym (make-symbol "#once")))
    `(progn
       (defun ,sym ()
         (remove-hook ,hook ',sym ,local)
         (funcall ,function))
       (add-hook ,hook ',sym ,append ,local))))
#+END_SRC
** tangle-this-config
I set this up to tangle the init org-mode file into the actual Emacs init file as soon as I save it.
#+BEGIN_SRC emacs-lisp
(defun tangle-init ()
  "If the current buffer is 'init.org' the code-blocks are
tangled, and the tangled file is compiled."
  (when (equal (buffer-file-name)
               (expand-file-name "~/Emacs/emacs_setup.org"))
    ;; Avoid running hooks when tangling.
    (let ((prog-mode-hook nil))
      (org-babel-tangle)
      (byte-compile-file "~/Emacs/emacs_setup.el"))))

(add-hook 'after-save-hook 'tangle-init)
#+END_SRC
** Clear C-p so I can use it as a prefix
Remove C-p that I want to use for *me* personally as a prefix.
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-p") nil) ;; was 'previous-line'
#+END_SRC

** Speedup VCS
Regexp matching directory names that are not under VC's control.
The default regexp prevents fruitless and time-consuming attempts
to determine the VC status in directories in which filenames are
interpreted as hostnames.
#+BEGIN_SRC emacs-lisp
(defvar locate-dominating-stop-dir-regexp
  "\\`\\(?:[\\/][\\/][^\\/]+\\|/\\(?:net\\|afs\\|\\.\\.\\.\\)/\\)\\'")
#+END_SRC
* Environment
** Browser default
#+BEGIN_SRC emacs-lisp
(setq browse-url-generic-program (executable-find "google-chrome")
  browse-url-browser-function 'browse-url-generic)
#+END_SRC
** UTF-8
 Make Emacs request UTF-8 first when pasting stuff.
#+BEGIN_SRC emacs-lisp
(use-package unicode-escape
  :ensure t
  :init
  (setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING)))
(set-language-environment "UTF-8")
#+END_SRC

* Buffer behavior
Use auto-revert, which reloads a file if it's updated on disk
and not modified in the buffer.
#+BEGIN_SRC emacs-lisp
(global-auto-revert-mode 1)
(put 'upcase-region 'disabled nil)
(put 'narrow-to-region 'disabled nil)
#+END_SRC

* Managing Buffers
** winner-mode (layout managing)
Enable winner-mode
Navigate buffer-window configs with C-c left and C-c right.
#+BEGIN_SRC emacs-lisp
(winner-mode 1)
#+END_SRC

** eyebrowse (layout managing)
This is supposed to be a better window manager.
#+BEGIN_SRC emacs-lisp
(use-package eyebrowse
  :ensure t)
#+END_SRC

** toggle-maximize-buffer
Temporarily maximize a buffer.
[[https://gist.github.com/mads379/3402786][found here]]
#+BEGIN_SRC emacs-lisp
(defun toggle-maximize-buffer () "Maximize buffer"
  (interactive)
  (if (= 1 (length (window-list)))
      (jump-to-register '_)
    (progn
      (window-configuration-to-register '_)
      (delete-other-windows))))
#+END_SRC

Map it to a key.
#+BEGIN_SRC emacs-lisp
(global-set-key [M-f8] 'toggle-maximize-buffer)
#+END_SRC
* Colors and Look
** Fontlock
This gets the font coloring switched on for all buffers.
*** TODO Note: this should be the default, maybe this can go ?
:LOGBOOK:
- State "TODO"       from              [2018-11-07 Wed 22:29]
:END:
#+BEGIN_SRC emacs-lisp
(global-font-lock-mode t)
#+END_SRC
** In terminal mode
#+BEGIN_SRC emacs-lisp
(when (display-graphic-p)
  (set-background-color "#ffffff")
  (set-foreground-color "#141312"))
#+END_SRC
** In X11 mode: mouse and window title
#+BEGIN_SRC emacs-lisp
(setq frame-title-format "emacs @ %b - %f")
(when window-system
  (mwheel-install)  ;; enable wheelmouse support by default
  (set-selection-coding-system 'compound-text-with-extensions))
#+END_SRC
** Look: buffer naming
#+BEGIN_SRC emacs-lisp
(use-package uniquify
  :init
  (setq uniquify-buffer-name-style 'post-forward-angle-brackets))
#+END_SRC
** Buffer Decorations
Setup the visual cues about the current editing buffer
#+BEGIN_SRC emacs-lisp
(column-number-mode 1)
(setq visible-bell t)
(setq scroll-step 1)
(setq-default transient-mark-mode t)  ;; highlight selection
#+END_SRC
** nyan-mode
#+BEGIN_SRC emacs-lisp
(use-package nyan-mode
  :ensure t
  :bind ("C-p n" . 'nyan-mode))
#+END_SRC
** dynamic cursor colors
The cursor is displayed in different colors, depending on overwrite or insert
mode.
#+BEGIN_SRC emacs-lisp
(setq hcz-set-cursor-color-color "")
(setq hcz-set-cursor-color-buffer "")

(defun hcz-set-cursor-color-according-to-mode ()
  "change cursor color according to some minor modes."
  ;; set-cursor-color is somewhat costly, so we only call it when needed:
  (let ((color
         (if buffer-read-only "orange"
           (if overwrite-mode "red"
             "green"))))
    (unless (and
             (string= color hcz-set-cursor-color-color)
             (string= (buffer-name) hcz-set-cursor-color-buffer))
      (set-cursor-color (setq hcz-set-cursor-color-color color))
      (setq hcz-set-cursor-color-buffer (buffer-name)))))

(add-hook 'post-command-hook 'hcz-set-cursor-color-according-to-mode)
#+END_SRC
* Key Mappings
** alternate key mappings
Letting one enter chars that are otherwise difficult in e.g. the minibuffer.
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-m") 'newline-and-indent)
(global-set-key (kbd "C-j") 'newline)
(global-set-key [delete] 'delete-char)
(global-set-key [kp-delete] 'delete-char)
#+END_SRC
** Macros
#+BEGIN_SRC emacs-lisp
(global-set-key [f3] 'start-kbd-macro)
(global-set-key [f4] 'end-kbd-macro)
(global-set-key [f5] 'call-last-kbd-macro)
#+END_SRC
** Text size
Increase/decrease text size
#+BEGIN_SRC emacs-lisp
(define-key global-map (kbd "C-+") 'text-scale-increase)
(define-key global-map (kbd "C--") 'text-scale-decrease)
#+END_SRC
** multiple regions
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-M-i") 'iedit-mode)
#+END_SRC
** Moving around buffers
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-c <C-left>")  'windmove-left)
(global-set-key (kbd "C-c <C-right>") 'windmove-right)
(global-set-key (kbd "C-c <C-up>")    'windmove-up)
(global-set-key (kbd "C-c <C-down>")  'windmove-down)
(global-set-key (kbd "C-c C-g") 'goto-line)
#+END_SRC
** multiple-cursors
Configure the shortcuts for multiple cursors
#+BEGIN_SRC emacs-lisp
(use-package multiple-cursors
  :ensure t
  :bind (("C-S-c C-S-c" . 'mc/edit-lines)
         ("C->" . 'mc/mark-next-like-this)
         ("C-<" . 'mc/mark-previous-like-this)
         ("C-c C->" . 'mc/mark-all-like-this)))
#+END_SRC
** ace-jump-mode
Let's one jump around text
#+BEGIN_SRC emacs-lisp
(use-package ace-jump-mode
  :ensure t
  :bind (("C-c SPC" . 'ace-jump-mode)
         ("C-c DEL" . 'ace-jump-mode-pop-mark)))
#+END_SRC
* Editing Style
** No tabs, ever. No traling spaces either.
#+BEGIN_SRC emacs-lisp
(setq-default indent-tabs-mode nil)
(setq require-final-newline t)
(setq next-line-add-newlines nil)
(add-hook 'before-save-hook 'delete-trailing-whitespace)
#+END_SRC
** Mark the 80 col boundary
#+BEGIN_SRC emacs-lisp
(use-package column-marker
  :config
  (add-hook 'c-mode-common-hook (lambda () (interactive) (column-marker-1 80)))
  :bind ("C-c m" . 'column-marker-1))
#+END_SRC
* Coding
** header/implementation toggle
Switch from header to implementation file quickly.
#+BEGIN_SRC emacs-lisp
(add-hook 'c-mode-common-hook
          (lambda ()
            (local-set-key  (kbd "C-c o") 'ff-find-other-file)))
#+END_SRC
** commenting out
Easy commenting out of lines
#+BEGIN_SRC emacs-lisp
(autoload 'comment-out-region "comment" nil t)
(global-set-key (kbd "C-c q") 'comment-out-region)
#+END_SRC

** Deduplicate and sort
Help cleanup the includes and using lists.
[[http://www.emacswiki.org/emacs/DuplicateLines][found here]]
#+BEGIN_SRC emacs-lisp
(defun uniquify-region-lines (beg end)
  "Remove duplicate adjacent lines in region."
  (interactive "*r")
  (save-excursion
    (goto-char beg)
    (while (re-search-forward "^\\(.*\n\\)\\1+" end t)
      (replace-match "\\1"))))

(defun fleury/sort-and-uniquify-region ()
  "Remove duplicates and sort lines in region."
  (interactive)
  (sort-lines nil (region-beginning) (region-end))
  (uniquify-region-lines (region-beginning) (region-end)))
#+END_SRC

Simplify cleanup of #include/typedef/using blocks.
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-p s") 'fleury/sort-and-uniquify-region)
#+END_SRC

** yankpad / yasnippet
#+BEGIN_SRC emacs-lisp
(use-package yasnippet
  :ensure t)

(use-package yankpad
  :ensure t
  :init
  (setq yankpad-file "~/OrgFiles/yankpad.org")
  :config
  (bind-key "<f7>" 'yankpad-map))
#+END_SRC

** Selective display
Will fold all text indented more than the position of the cursor at the time the
keys are pressed.
#+BEGIN_SRC emacs-lisp
(defun set-selective-display-dlw (&optional level)
  "Fold text indented more than the cursor.
   If level is set, set the indent level to level.
   0 displays the entire buffer."
  (interactive "P")
  (set-selective-display (or level (current-column))))

(global-set-key "\C-x$" 'set-selective-display-dlw)
#+END_SRC
** Info in the gutter
*** Line numbers
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-c C-n") 'linum-mode)
#+END_SRC
*** git informations
#+BEGIN_SRC emacs-lisp
(use-package git-gutter-fringe+
  :ensure t
  :bind ("C-c g" . 'git-gutter+-mode))
#+END_SRC
** GDB with many windows
#+BEGIN_SRC emacs-lisp
(setq gdb-many-windows t)
#+END_SRC
* Cool Packages
** magit
Add the powerful Magit
#+BEGIN_SRC emacs-lisp
(use-package magit
  :ensure t
  :config
  (global-set-key (kbd "C-x g") 'magit-status))
(use-package magit-todos
   :ensure t)
#+END_SRC

** annotate-mode
file-annotations store externally.
Seems to fail with args-out-of-range and then Emacs is confused.
(filed issue for this)

Also, it seems to interfere with colorful modes like magit or org-agenda-mode
#+BEGIN_SRC emacs-lisp
(use-package annotate
  :ensure t
  :bind ("C-c C-A" . 'annotate-annotate)  ;; for ledger-mode
  :config
  (add-hook 'org-mode 'annotate-mode)
  (add-hook 'csv-mode 'annotate-mode)
  (add-hook 'c-mode 'annotate-mode)
  (add-hook 'c++-mode 'annotate-mode)
  (add-hook 'sh-mode 'annotate-mode)
  (add-hook 'ledger-mode 'annotate-mode)
;;;  (define-globalized-minor-mode global-annotate-mode annotate-mode
;;;    (lambda () (annotate-mode 1)))
;;;  (global-annotate-mode 1)
  )
#+END_SRC

** icicles (list completion)
Enhanced minibuffer completion if we have icicles
#+BEGIN_SRC emacs-lisp
(use-package icicles
  :ensure t
  :config
  (icy-mode 1))
#+END_SRC

** web-mode
web-mode with config for Polymer editing
#+BEGIN_SRC emacs-lisp
(use-package web-mode
  :ensure t
  :mode "\\.html\\'"
  :config
  (setq web-mode-markup-indent-offset 2)
  (setq web-mode-css-indent-offset 2)
  (setq web-mode-code-indent-offset 2))
#+END_SRC
** emmet-mode
#+BEGIN_SRC emacs-lisp
(use-package emmet-mode
:ensure t)
#+END_SRC
** rainbow-mode
#+BEGIN_SRC emacs-lisp
(use-package rainbow-mode
:ensure t)
#+END_SRC
** taskjuggler-mode (tj3-mode)
#+BEGIN_SRC emacs-lisp
(use-package tj3-mode
 :ensure t)
#+END_SRC

** writeroom-mode
#+BEGIN_SRC emacs-lisp
(use-package writeroom-mode
  :init
  (global-set-key (kbd "C-p w") 'writeroom-mode))
#+END_SRC

** wgrep-mode
#+BEGIN_SRC emacs-lisp
(use-package wgrep
  :ensure t)
#+END_SRC

** ledger-mode
*** Cleanup ledger file
#+BEGIN_SRC emacs-lisp
(defun single-lines-only ()
  "replace multiple blank lines with a single one"
  (interactive)
  (goto-char (point-min))
  (while (re-search-forward "\\(^\\s-*$\\)\n" nil t)
    (replace-match "\n")
    (forward-char 1)))

(defun paf/cleanup-ledger-buffer ()
  "Cleanup the ledger file"
  (interactive)
  (delete-trailing-whitespace)
  (single-lines-only)
  (ledger-mode-clean-buffer)
  (ledger-sort-buffer))
#+END_SRC
*** Setup
#+BEGIN_SRC emacs-lisp
(use-package ledger-mode
  :ensure t
  :mode "\\.ledger\\'"
  :bind ("<f6>" . 'paf/cleanup-ledger-buffer)
  :config
  (setq ledger-reconcile-default-commodity "CHF"))
#+END_SRC
* OrgMode
Load all my org stuff, but first org-mode itself.
** Init
If variable `org-directory` not set yet, map it to my home's files.
You may set this in the ~/.emacs to another value, e.g.
=(setq org-directory "/ssh:fleury@bernina.c.googlers.com:OrgFiles")=
#+BEGIN_SRC emacs-lisp
(require 'org)
(if (not (boundp 'org-directory))
    (setq org-directory "~/OrgFiles"))
#+END_SRC
** Helper Functions / Tools found on the web / worg
*** Org-relative file function
#+BEGIN_SRC emacs-lisp
(defun org-relative-file (filename)
  "Compute an expanded absolute file path for org files"
  (expand-file-name filename org-directory))
#+END_SRC
*** Preserve structure in archives
Make sure archiving preserves the same tree structure, including when
archiving subtrees.
[[https://orgmode.org/worg/org-hacks.html#org4265b4c][source on worg]]
#+BEGIN_SRC emacs-lisp
(defun my-org-inherited-no-file-tags ()
  (let ((tags (org-entry-get nil "ALLTAGS" 'selective))
        (ltags (org-entry-get nil "TAGS")))
    (mapc (lambda (tag)
            (setq tags
                  (replace-regexp-in-string (concat tag ":") "" tags)))
          (append org-file-tags (when ltags (split-string ltags ":" t))))
    (if (string= ":" tags) nil tags)))

(defadvice org-archive-subtree
    (around my-org-archive-subtree-low-level activate)
  (let ((tags (my-org-inherited-no-file-tags))
        (org-archive-location
         (if (save-excursion (org-back-to-heading)
                             (> (org-outline-level) 1))
             (concat (car (split-string org-archive-location "::"))
                     "::* "
                     (car (org-get-outline-path)))
           org-archive-location)))
    ad-do-it
    (with-current-buffer (find-file-noselect (org-extract-archive-file))
      (save-excursion
        (while (org-up-heading-safe))
        (org-set-tags tags)))))
#+END_SRC
*** Adjust tags on the right
Dynamically adjust tag position
[[https://orgmode.org/worg/org-hacks.html#org0560357][source on worg]]

#+BEGIN_SRC emacs-lisp
(defun ba/org-adjust-tags-column-reset-tags ()
  "In org-mode buffers it will reset tag position according to
`org-tags-column'."
  (when (and
         (not (string= (buffer-name) "*Remember*"))
         (eql major-mode 'org-mode))
    (let ((b-m-p (buffer-modified-p)))
      (condition-case nil
          (save-excursion
            (goto-char (point-min))
            (command-execute 'outline-next-visible-heading)
            ;; disable (message) that org-set-tags generates
            (flet ((message (&rest ignored) nil))
              (org-set-tags 1 t))
            (set-buffer-modified-p b-m-p))
        (error nil)))))

(defun ba/org-adjust-tags-column-now ()
  "Right-adjust `org-tags-column' value, then reset tag position."
  (set (make-local-variable 'org-tags-column)
       (- (- (window-width) (length org-ellipsis))))
  (ba/org-adjust-tags-column-reset-tags))

(defun ba/org-adjust-tags-column-maybe ()
  "If `ba/org-adjust-tags-column' is set to non-nil, adjust tags."
  (when ba/org-adjust-tags-column
    (ba/org-adjust-tags-column-now)))

(defun ba/org-adjust-tags-column-before-save ()
  "Tags need to be left-adjusted when saving."
  (when ba/org-adjust-tags-column
     (setq org-tags-column 1)
     (ba/org-adjust-tags-column-reset-tags)))

(defun ba/org-adjust-tags-column-after-save ()
  "Revert left-adjusted tag position done by before-save hook."
  (ba/org-adjust-tags-column-maybe)
  (set-buffer-modified-p nil))

;; between invoking org-refile and displaying the prompt (which
;; triggers window-configuration-change-hook) tags might adjust,
;; which invalidates the org-refile cache
(defadvice org-refile (around org-refile-disable-adjust-tags)
  "Disable dynamically adjusting tags"
  (let ((ba/org-adjust-tags-column nil))
    ad-do-it))
(ad-activate 'org-refile)

;; Now set it up
(setq ba/org-adjust-tags-column t)
;; automatically align tags on right-hand side
;; TODO(fleury): Does not seem to work as of 2017/12/18
;; Seems to work again 2018/11/01
(add-hook 'window-configuration-change-hook
          'ba/org-adjust-tags-column-maybe)
(add-hook 'before-save-hook 'ba/org-adjust-tags-column-before-save)
(add-hook 'after-save-hook 'ba/org-adjust-tags-column-after-save)
(add-hook 'org-agenda-mode-hook (lambda ()
                                  (setq org-agenda-tags-column (- (window-width)))))
#+END_SRC
**** TODO Update =flet= and =org-set-tags=
:LOGBOOK:
- State "TODO"       from              [2019-01-12 Sat 12:08]
:END:
=flet= as well as =org-set-tags= with > 1 args are deprecated.
Not sure what to replace it with though...

*** Auto-Refresh Agenda
Refresh org-mode agenda regularly.
[[https://orgmode.org/worg/org-hacks.html#orgab827a7][source on worg]]
There are two functions that supposedly do the same.
#+BEGIN_SRC emacs-lisp
(defun kiwon/org-agenda-redo-in-other-window ()
  "Call org-agenda-redo function even in the non-agenda buffer."
  (interactive)
  (let ((agenda-window (get-buffer-window org-agenda-buffer-name t)))
    (when agenda-window
      (with-selected-window agenda-window (org-agenda-redo)))))

(defun update-agenda-if-visible ()
  (interactive)
  (let ((buf (get-buffer "*Org Agenda*"))
        wind)
    (if buf
        (org-agenda-redo))))
#+END_SRC
*** Display Agenda when idle
Show the agenda when emacs left idle.
[[https://orgmode.org/worg/org-hacks.html#orgaea636d][source on worg]]
#+BEGIN_SRC emacs-lisp
(defun jump-to-org-agenda ()
  (interactive)
  (let ((buf (get-buffer "*Org Agenda*"))
        wind)
    (if buf
        (if (setq wind (get-buffer-window buf))
            (select-window wind)
          (if (called-interactively-p 'any)
              (progn
                (select-window (display-buffer buf t t))
                (org-fit-window-to-buffer)
                (org-agenda-redo)
                )
            (with-selected-window (display-buffer buf)
              (org-fit-window-to-buffer)
              ;;(org-agenda-redo)
              )))
      (call-interactively 'org-agenda-list)))
  ;;(let ((buf (get-buffer "*Calendar*")))
  ;;  (unless (get-buffer-window buf)
  ;;    (org-agenda-goto-calendar)))
  )
#+END_SRC
*** Properties collector
Collect properties into tables.
See documentation in the file.
#+BEGIN_SRC emacs-lisp
(load-file "~/Emacs/org-collector.el")
#+END_SRC
** My Setup
These are mostly org-config specific to me, myself and I.
*** Key mappings
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-c l") 'org-store-link)
(global-set-key (kbd "C-c c") 'org-capture)
(global-set-key (kbd "C-c a") 'org-agenda)
(global-set-key (kbd "C-c b") 'org-iswitchb)

(add-hook 'org-mode-hook
    (lambda ()
            (local-set-key (kbd "C-<up>") 'org-move-subtree-up)
            (local-set-key (kbd "C-<down>") 'org-move-subtree-down)))

#+END_SRC
*** Display settings
Some config for display.
#+BEGIN_SRC emacs-lisp
(setq org-hide-leading-stars 't)
(setq org-log-done 't)
(setq org-startup-folded 't)
(setq org-startup-indented 't)
(setq org-startup-folded 't)
(setq org-ellipsis "...")
; Don't really like the new bullets though.
;;(use-package 'org-bullets
;;  :config
;;  (add-hook 'org-mode-hook (lambda () (org-bullets-mode 1))))
#+END_SRC
*** org-habit
#+BEGIN_SRC emacs-lisp
(use-package org-habit
  :config
  (setq org-habit-graph-column 45)
  (setq org-habit-preceding-days 90)
  (setq org-habit-following-days 30)
  (setq org-habit-show-habits-only-for-today nil))
#+END_SRC
*** bash command
#+BEGIN_SRC emacs-lisp
(setq org-babel-sh-command "bash")
#+END_SRC
*** org-clock properties
clock stuff into a drawer.
#+BEGIN_SRC emacs-lisp
(setq org-clock-into-drawer t)
(setq org-log-into-drawer t)
(setq org-clock-int-drawer "CLOCK")
#+END_SRC
*** org-mobile
config for org-mobile-*
#+BEGIN_SRC emacs-lisp
(setq org-mobile-directory (org-relative-file "Mobile"))
(setq org-mobile-inbox-for-pull (org-relative-file "mobileorg.org"))
#+END_SRC
*** open first agenda file
F12 open the first agenda file
#+BEGIN_SRC emacs-lisp
(defun org-get-first-agenda-file ()
  (interactive)
  (find-file (elt org-agenda-files 0)))
(global-set-key [f12] 'org-get-first-agenda-file)
; F12 on Mac OSX displays the dashboard....
(global-set-key [C-f12] 'org-get-first-agenda-file)
#+END_SRC
*** org-ehtml [localhost:55555]
This will start serving the org files through the emacs-based webbrowser
when pressing M-f12 (on localhost:55555)
#+BEGIN_SRC emacs-lisp
(use-package org-ehtml
  :config
  (setq org-ehtml-docroot (expand-file-name org-directory))
  (setq org-ehtml-everything-editable t)
  (setq org-ehtml-allow-agenda t))

(defun fleury/start-web-server ()
  (interactive)
  (ws-start org-ehtml-handler 55555))
(global-set-key (kbd "<M-f12>") 'fleury/start-web-server)
#+END_SRC
*** org-link-abbrev
This lets one write links as e.g. [ [b:123457] ]
#+BEGIN_SRC emacs-lisp
(setq org-link-abbrev-alist
      '(("b" . "http://b/")
        ("go" . "http://go/")
        ("cl" . "http://cr/")))
#+END_SRC
*** org-secretary
#+BEGIN_SRC emacs-lisp
(use-package org-secretary
:config
(setq org-sec-me "paf")
(setq org-tag-alist '(("PRJ" . ?p)
                      ("DESIGNDOC" . ?D)
                      ("Milestone" . ?m)
                      ("DESK" . ?d)
                      ("HOME" . ?h)
                      ("VC" . ?v))))
#+END_SRC
*** task tracking
Track task dependencies, and dim them in in the agenda.
#+BEGIN_SRC emacs-lisp
(setq org-enforce-todo-dependencies t)
(setq org-agenda-dim-blocked-tasks 'invisible)
#+END_SRC
*** effort & columns mode
#+BEGIN_SRC emacs-lisp
(setq org-global-properties
      '(("Effort_ALL". "0 0:10 0:30 1:00 2:00 4:00 8:00 16:00")))
(setq org-columns-default-format
      "%TODO %30ITEM %3PRIORITY %6Effort{:} %10DEADLINE")
#+END_SRC
*** org-todo keywords
#+BEGIN_SRC emacs-lisp
(setq org-todo-keywords
      '((sequence "TODO(t!)" "NEXT(n!)" "STARTED(s!)" "WAITING(w!)" "AI(a!)" "|" "DONE(d!)" "CANCELLED(C@)" "DEFERRED(D@)" "SOMEDAY(S!)" "FAILED(F!)" "REFILED(R!)")
        (sequence "APPLIED(A!)" "WAITING(w!)" "ACCEPTED" "|" "REJECTED" "PUBLISHED")
        (sequence "TASK(m!)" "|" "DONE(d!)" "CANCELLED(C@)" )))

(setq org-tags-exclude-from-inheritance '("PRJ")
      org-use-property-inheritance '("PRIORITY")
      org-stuck-projects '("+PRJ/-DONE-CANCELLED"
			   ; it is considered stuck if there is no next action
                           (;"TODO"
			    "NEXT" "STARTED" "TASK") ()))

(setq org-todo-keyword-faces '(
        ("TODO" . (:foreground "purple" :weight bold))
        ("TASK" . (:foreground "steelblue" :weight bold))
        ("NEXT" . (:foreground "red" :weight bold))
        ("STARTED" . (:foreground "darkgreen" :weight bold))
        ("WAITING" . (:foreground "orange" :weight bold))
        ("FLAG_GATED" . (:foreground "orange" :weight bold))
        ("SOMEDAY" . (:foreground "steelblue" :weight bold))
        ("MAYBE" . (:foreground "steelblue" :weight bold))
        ("AI" . (:foreground "red" :weight bold))
        ("NEW" . (:foreground "orange" :weight bold))
        ("RUNNING" . (:foreground "orange" :weight bold))
        ("WORKED" . (:foreground "green" :weight bold))
        ("FAILED" . (:foreground "red" :weight bold))
        ("REFILED" . (:foreground "gray"))
        ; For publications
        ("APPLIED" . (:foreground "orange" :weight bold))
        ("ACCEPTED" . (:foreground "orange" :weight bold))
        ("REJECTED" . (:foreground "red" :weight bold))
        ("PUBLISHED" . (:foreground "green" :weight bold))
        ))
#+END_SRC
*** org-agenda
**** views
#+BEGIN_SRC emacs-lisp
(setq org-agenda-custom-commands
       '(("t" "Hot Today" ((agenda "" ((org-agenda-span 'day)))
                           (tags-todo "/NEXT")
                           (tags-todo "-dowith={.+}/STARTED")
                           (tags-todo "-dowith={.+}/WAITING")))
         ("n" "Agenda and all TODO's" ((agenda "") (alltodo "")))
         ("N" "Next actions" tags-todo
          "-personal-doat={.+}-dowith={.+}/!-TASK-TODO"
          ((org-agenda-todo-ignore-scheduled t)))
         ("h" "Work todos" tags-todo
          "-personal-doat={.+}-dowith={.+}/!-TASK"
          ((org-agenda-todo-ignore-scheduled t)))
         ("H" "All work todos" tags-todo "-personal/!-TASK-CANCELLED"
          ((org-agenda-todo-ignore-scheduled nil)))
         ("A" "Work todos with doat or dowith" tags-todo
          "-personal+doat={.+}|dowith={.+}/!-TASK"
          ((org-agenda-todo-ignore-scheduled nil)))
         ("j" "TODO dowith and TASK with"
         ((org-sec-with-view "TODO dowith")
          (org-sec-where-view "TODO doat")
          (org-sec-assigned-with-view "TASK with")
          (org-sec-stuck-with-view "STUCK with")
          (todo "STARTED")))
         ("J" "Interactive TODO dowith and TASK with"
          ((org-sec-who-view "TODO dowith")))))
#+END_SRC
**** colors
Make the calendar day info a bit more visible.
Move this into the custom-set-faces in ~/.emacs
#+BEGIN_SRC emacs-lisp
;; '(org-agenda-date ((t (:inherit org-agenda-structure :background "pale green" :foreground "black" :weight bold))) t)
;; '(org-agenda-date-weekend ((t (:inherit org-agenda-date :background "light blue" :weight bold))) t)
;; '(org-agenda-current-time ((t (:inherit org-time-grid :foreground "yellow" :weight bold))))
#+END_SRC
**** now marker
A more visible current-time marker in the agenda
#+BEGIN_SRC emacs-lisp
(setq org-agenda-current-time-string ">>>>>>>>>> NOW <<<<<<<<<<")
#+END_SRC
**** auto-refresh
#+BEGIN_SRC emacs-lisp
  ;; will refresh it only if already visible
  ;;(run-at-time nil 180 'update-agenda-if-visible)
  (add-hook 'org-mode-hook
            (lambda () (run-at-time nil 180 'kiwon/org-agenda-redo-in-other-window)))

  ;; Make this happen only if we open an org file.
  (add-hook 'org-mode-hook
            (lambda () (run-with-idle-timer 600 t 'jump-to-org-agenda)))
#+END_SRC
**** auto-save org files when idle
This will save them regularly when the idle for more than a minute.
#+BEGIN_SRC emacs-lisp
(add-hook 'org-mode-hook
    (lambda () (run-with-idle-timer 60 t 'org-save-all-org-buffers)))
#+END_SRC
**** export
That's the export function to update the agenda view.
#+BEGIN_SRC emacs-lisp :tangle no
(setq org-agenda-exporter-settings
      '((ps-number-of-columns 2)
        (ps-portrait-mode t)
        (org-agenda-add-entry-text-maxlines 5)
        (htmlize-output-type 'font)))

(defun dmg-org-update-agenda-file (&optional force)
  (interactive)
  (save-excursion
    (save-window-excursion
      (let ((file "~/www/agenda/agenda.html"))
        (org-agenda-list)
        (org-agenda-write file)))))
#+END_SRC
*** org-duration
#+BEGIN_SRC emacs-lisp
(use-package org-duration
  :config
  (setq org-duration-units
    `(("min" . 1)
      ("h" . 60)
      ("d" . ,(* 60 8))
      ("w" . ,(* 60 8 5))
      ("m" . ,(* 60 8 5 4))
      ("y" . ,(* 60 8 5 4 10)))
     )
  (org-duration-set-regexps))
#+END_SRC
*** org-refile
Capture and refile stuff, with some templates that I think are useful.
#+BEGIN_SRC emacs-lisp
(setq org-default-notes-file (org-relative-file "refile.org"))

(setq org-capture-templates
      `(("a" "Action Item" entry (file+headline ,(org-relative-file "refile.org") "Tasks")
             "* AI %?\n  %U")
        ("m" "Meeting" entry (file+headline ,(org-relative-file "refile.org") "Meetings")
             "* %U  :MTG:\n %^{with}p\n%?")
        ("n" "Note" entry (file+headline ,(org-relative-file "refile.org") "Notes")
             "* %?\n%U")
        ("j" "Journal" entry (file+datetree ,(org-relative-file "journal.org"))
             "* %?\n  %U")))

; show up to 2 levels for refile targets, in all agenda files
(setq org-refile-targets '((org-agenda-files . (:maxlevel . 2))))
(setq org-log-refile t)  ;; will add timestamp when refiled.
; from: http://doc.norang.ca/org-mode.html
; Exclude DONE state tasks from refile targets
(defun bh/verify-refile-target ()
  "Exclude todo keywords with a done state from refile targets"
  (not (member (nth 2 (org-heading-components)) org-done-keywords)))
(setq org-refile-target-verify-function 'bh/verify-refile-target)
#+END_SRC
*** org-babel
What kind of code block languages do I need
#+BEGIN_SRC emacs-lisp
(setq org-confirm-babel-evaluate 'nil) ; Don't ask before executing

(org-babel-do-load-languages
 'org-babel-load-languages
 '(
   (R . t)
   (dot . t)
   (emacs-lisp . t)
   (gnuplot . t)
   (python . t)
   (ledger . t)
   ;;(sh . t)
   (latex . t)
   (plantuml . t)
   (shell . t)
  ))
#+END_SRC
*** org-export
Add the ODT as an export format
#+BEGIN_SRC emacs-lisp
(use-package ox-odt)
(use-package ox-reveal) ; under review
(use-package ox-taskjuggler)
#+END_SRC
*** plant-uml
Tell where PlantUML is to be found
#+BEGIN_SRC emacs-lisp
(setq org-plantuml-jar-path (expand-file-name "~/Apps/plantuml.jar"))
#+END_SRC
*** iimage (M-I)
Make the display of images a simple key-stroke away.
#+BEGIN_SRC emacs-lisp
(use-package iimage
  :config
  (add-to-list 'iimage-mode-image-regex-alist
               (cons (concat "\\[\\[file:\\(~?" iimage-mode-image-filename-regex
                             "\\)\\]")  1))

  (defun org-toggle-iimage-in-org ()
    "display images in your org file"
    (interactive)
    (if (face-underline-p 'org-link)
        (set-face-underline 'org-link nil)
      (set-face-underline 'org-link t))
    (iimage-mode 'toggle))

  (add-hook 'org-mode-hook (lambda ()
                             ;; display images
                             (local-set-key "\M-I" 'org-toggle-iimage-in-org)
                            )))
#+END_SRC
